terraform {
  required_providers {
    null = {
      source  = "hashicorp/null"
      version = "3.2.2"
    }
  }
}

/*
* Generate a ~/.ssh/iac-kubeadm-deployment_config file in the user's home directory with an alias and a specified public key
* for passwordless SSH using the alias (e.g., ssh k8s-master-00).
*/
resource "local_file" "ssh_config" {
  content = templatefile("${path.root}/../../templates/ssh_config.tftpl", {
    nodes                = var.all_nodes,
    ssh_user             = var.vm_username,
    ssh_private_key_path = var.ssh_private_key_path
  })
  filename        = pathexpand("~/.ssh/iac-kubeadm-deployment_config")
  file_permission = "0600"
}

/*
* NOTE: Call functions in `utils_ssh.sh` via local-exec to manage the ~/.ssh/config file. 
* This avoids deletion during `terraform destroy()` in `scripts/terraform.sh`.
*/
resource "null_resource" "ssh_config_include" {
  depends_on = [local_file.ssh_config]

  # Re-run when the content of the ssh_config changes
  triggers = {
    ssh_config_content = local_file.ssh_config.content
  }

  provisioner "local-exec" {
    command     = ". ${path.root}/../../../scripts/utils_ssh.sh && integrate_ssh_config"
    interpreter = ["/bin/bash", "-c"]
  }

  provisioner "local-exec" {
    when        = destroy
    command     = ". ${path.root}/../../../scripts/utils_ssh.sh && deintegrate_ssh_config"
    interpreter = ["/bin/bash", "-c"]
  }
}

/*
* This makes sure this resource runs only after the "for_each" loop
* in "configure_nodes" has completed for all nodes.
*/
resource "null_resource" "prepare_ssh_access" {
  depends_on = [var.vm_status, null_resource.ssh_config_include]

  provisioner "local-exec" {
    command     = <<-EOT
      set -e
      echo ">>> Verifying VM liveness and preparing SSH access..."
      . ${path.root}/../../../scripts/utils_ssh.sh
      bootstrap_ssh_known_hosts ${join(" ", [for node in var.all_nodes : node.ip])}
      echo ">>> Liveness check passed. SSH access is ready."
    EOT
    interpreter = ["/bin/bash", "-c"]
  }
}

/*
* Generate the Ansible inventory file from template
*/
resource "local_file" "inventory" {
  content = templatefile("${path.root}/../../templates/inventory.yaml.tftpl", {
    master_nodes          = [for node in var.all_nodes : node if startswith(node.key, "k8s-master")],
    worker_nodes          = [for node in var.all_nodes : node if startswith(node.key, "k8s-worker")],
    k8s_master_ips        = var.k8s_master_ips,
    k8s_ha_virtual_ip     = var.k8s_ha_virtual_ip,
    k8s_pod_subnet        = var.k8s_pod_subnet,
    ansible_ssh_user      = var.vm_username,
    k8s_pod_subnet_prefix = var.k8s_pod_subnet_prefix
  })
  filename        = "${var.ansible_path}/inventory.yaml"
  file_permission = "0644"
}

resource "null_resource" "provision_cluster" {

  depends_on = [null_resource.prepare_ssh_access, local_file.inventory]

  provisioner "local-exec" {

    working_dir = abspath("${path.root}/../../../")

    /*
     * To avoid "(output suppressed due to sensitive value in config)" shown in terminal
     * Use `nonsensitive()` function to decrypt the playbook log.
     * `rm-rf` is to ensure the destination directory for fetched files exists and is clean
    */
    command = <<-EOT
      set -e
      rm -f ${var.ansible_path}/fetched/kubeconfig
      mkdir -p ${var.ansible_path}/fetched

      ansible-playbook \
        -i ${var.ansible_path}/inventory.yaml \
        --private-key ${nonsensitive(var.ssh_private_key_path)} \
        --extra-vars "ansible_ssh_user=${nonsensitive(var.vm_username)}" \
        -v \
        ${var.ansible_path}/playbooks/10-provision-cluster.yaml
    EOT

    interpreter = ["/bin/bash", "-c"]
  }
}

/*
 * Use an external data source to read files generated by Ansible provisioner
*/
data "external" "fetched_kubeconfig" {

  depends_on = [null_resource.provision_cluster]
  program = ["/bin/bash", "-c", <<-EOT
    set -e
    KUBECONFIG_PATH="${var.ansible_path}/fetched/kubeconfig"
    if [ ! -f "$KUBECONFIG_PATH" ]; then
      echo '{}'
      exit 0
    fi

    jq -n --arg kc "$(cat $KUBECONFIG_PATH)" '{"content": $kc}'
  EOT
  ]
}
